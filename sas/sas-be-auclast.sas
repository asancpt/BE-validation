DATA BE; /* It will load 91 records. */
  INFILE 'c:\Users\mdlhs\asancpt\BEreport\sas\NCAResult4BE.csv' FIRSTOBS=2 DLM=",";
  INPUT SUBJ $ SEQ $ PRD $ TRT $ AUClast Cmax Tmax;
  IF CMAX =< 0 THEN DELETE;
  LNCMAX = LOG(Cmax);
  LNAUCL = LOG(AUClast );

PROC PRINT; RUN;

PROC GLM DATA=BE OUTSTAT=STATRES; /* GLM use only complete subjects. */
  CLASS SEQ PRD TRT SUBJ;
  MODEL LNAUCL = SEQ SUBJ(SEQ) PRD TRT;
  RANDOM SUBJ(SEQ)/TEST;
  LSMEANS TRT /PDIFF=CONTROL('R') CL ALPHA=0.1 COV OUT=LSOUT;
RUN;

PROC PRINT DATA=STATRES; RUN;
PROC PRINT DATA=LSOUT; RUN;

DATA STATRES;
  SET STATRES;
  IF _TYPE_='ERROR' THEN CALL SYMPUT('DF', DF);

DATA LSOUT;
  SET LSOUT;
  IF TRT='R' THEN CALL SYMPUT('GMR_R', LSMEAN);
  IF TRT='T' THEN CALL SYMPUT('GMR_T', LSMEAN);
  IF TRT='R' THEN CALL SYMPUT('V_R', COV1);
  IF TRT='T' THEN CALL SYMPUT('V_T', COV2);
  IF TRT='T' THEN CALL SYMPUT('COV', COV1);

DATA LSOUT2;
  LNPE = &GMR_T - &GMR_R;
  DF = &DF;
  SE = SQRT(&V_R + &V_T - 2*&COV);
  LNLM = TINV(0.95, DF)*SE;
  LNLL = LNPE - LNLM ;
  LNUL = LNPE + LNLM;
  PE = EXP(LNPE);
  LL = EXP(LNLL);
  UL = EXP(LNUL);
  WD = UL - LL;

PROC PRINT DATA=LSOUT2; RUN;

PROC MIXED DATA=BE; /* MIXED  uses all data. */
  CLASS SEQ TRT SUBJ PRD;
  MODEL LNAUCL = SEQ PRD TRT;
  RANDOM SUBJ(SEQ);
  ESTIMATE 'T VS R' TRT -1 1 /CL ALPHA=0.1; 
  ODS OUTPUT ESTIMATES=ESTIM COVPARMS=COVPAR;
RUN;

DATA COVPAR;
  SET COVPAR;
  IF CovParm = 'Residual' THEN CALL SYMPUT('MSE', Estimate);

DATA ESTIM;
  SET ESTIM;
  MSE = &MSE;
  LNLM = (Upper - Lower)/2;
  PE = EXP(Estimate);
  LL = EXP(Lower);
  UL = EXP(Upper);
  WD = UL - LL;

PROC PRINT Data=ESTIM; RUN;

